PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
 ?uri a owl:Class;
      skos:prefLabel ?label
}
WHERE {

?uri rdfs:label ?uberon_label ;
     a owl:Class .

{
SELECT ?uri ?fma_uri WHERE {
{
 {
  ?uri oboInOwl:hasDbXref ?xref 
 }
UNION
 {
  ?axiom a owl:Axiom;
  	owl:annotatedSource ?uri;
        oboInOwl:hasDbXref ?xref .
 }
}
FILTER isLiteral(?xref)
FILTER regex( ?xref, "^FMA", "i") 
BIND ( iri (concat ( "http://purl.obolibrary.org/obo/", replace(?xref, "FMA:", "FMA_"))) as ?fma_uri)
}
}

OPTIONAL {?fma_uri rdfs:label ?fma_label}

BIND ( if(  exists {?fma_uri rdfs:label ?fma_label }, ?fma_label, ?uberon_label) as ?label)

}
